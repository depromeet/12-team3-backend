plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

// application 레이어는 executable jar 만 생성
jar.enabled = false

configurations {
    asciidoctorExt
}

bootJar {
    dependsOn asciidoctor
}

ext {
    snippetsDir = file('build/generated-snippets')
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}

asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

task copyDocument(type: Copy) {
    dependsOn asciidoctor
    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

build {
    dependsOn copyDocument
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

dependencies {
    implementation project(":domain")
    implementation project(":inner-system")
    implementation project(":common")

    //	implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    testImplementation 'org.springframework.security:spring-security-test'
}